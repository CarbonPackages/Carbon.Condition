// Helper for checking if the element should get rendered or not

prototype(Carbon.Condition:Properties) < prototype(Neos.Fusion:Value) {
    node = ${node}
    live = false
    backend = true
    // Comma seperated string
    properties = false
    needAllProperties = true

    @context {
        node = ${this.node}
        live = ${this.live}
        backend = ${this.backend}
        needAllProperties = ${this.needAllProperties}
        properties = ${this.properties}
    }

    value = Neos.Fusion:Case {
        inBackend {
            condition = ${backend && node.context.inBackend}
            renderer = true
        }
        isLive {
            condition = ${live && node.context.live}
            renderer = true
        }
        properties {
            condition = ${Type.isString(properties)}
            renderer = Neos.Fusion:Value {
                array = Neos.Fusion:RawCollection {
                    collection = ${String.split(String.replace(properties, ' ', ''), ',')}
                    itemRenderer = ${q(node).property(item) ? true : false}
                }
                value = ${needAllProperties ? Array.indexOf(this.array, false) == -1 : Array.indexOf(this.array, true) != -1}
            }
        }
        default {
            condition = true
            renderer = false
        }
    }
}
