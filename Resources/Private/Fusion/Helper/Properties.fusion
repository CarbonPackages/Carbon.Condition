// Helper for checking if the element should get rendered or not

prototype(Carbon.Condition:Properties) < prototype(Neos.Fusion:Case) {
    node = ${node}

    // Comma seperated string
    properties = null
    // can be OR, AND, &&, ||
    operator = 'AND'
    operator.@process.trimAndLowerCase = ${String.trim(String.toLowerCase(value))}

    needAllProperties = ${this.operator == 'and' || this.operator == '&&' ? true : false}

    context = Neos.Fusion:RawArray {
        backend = true
        live = false
    }

    @context {
        node = ${this.node}
        properties = ${this.properties}
        needAllProperties = ${this.needAllProperties}
        context = ${this.context}
    }

    @ignoreProperties = ${['node','properties','operator','needAllProperties','context']}

    correctContext {
        condition = ${(context.live && node.context.live) || (context.backend && node.context.inBackend)}
        renderer = true
    }
    hasProperties {
        condition = ${Type.isString(properties)}
        renderer = Neos.Fusion:Value {
            array = Neos.Fusion:RawCollection {
                collection = ${String.split(String.replace(properties, ' ', ''), ',')}
                itemRenderer = ${q(node).property(item) ? true : false}
            }
            value = ${needAllProperties ? Array.indexOf(this.array, false) == -1 : Array.indexOf(this.array, true) != -1}
        }
    }
    default {
        condition = true
        renderer = false
    }
}
